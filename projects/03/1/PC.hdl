// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/1/PC.hdl

/**
 * 16-bit counter with load and reset controls.
 *
 * If reset(t-1) then out(t) = 0
 *    else if load(t-1) then out(t) = in(t-1)
 *         else if inc(t-1) then out(t) = out(t-1) + 1 (integer addition)
 *              else out(t) = out(t-1)
 */

CHIP PC {

    IN  in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    
    Inc16(in=res, out=incres);

    //Multiplex all the options for output
    Mux16(sel=inc, a=res, b=incres, out=mux1);
    Mux16(sel=load, a=mux1, b=in, out=mux2);
    Mux16(sel=reset, a=mux2, b=false, out=mux3);

    Register(in=mux3, load=true, out=out, out=res);
}
